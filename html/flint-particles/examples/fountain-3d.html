<!DOCTYPE html>
<html lang="en">
<head>
<meta http-equiv="Content-Type" content="text/html; charset=UTF-8" />
<meta name="viewport" content="width=device-width, initial-scale=1" />
<title>Flint Particles Fountain</title>
<link rel="stylesheet" type="text/css" media="all" href="/style.css" />
<link rel="stylesheet" type="text/css" media="all" href="/flint-particles/style.css" />
<link rel="icon" href="/favicon.ico" type="image/x-icon" />
<link rel="shortcut icon" href="/favicon.ico" type="image/x-icon" />
</head>

<body>
<div id="page">
<header role="banner">

	<div id="branding"><img id="site-image" src="/flint-particles/images/core/logobig.gif" width="325" height="37" /></div>

	<nav id="menu" role="navigation">
	<div>
		<ul>
			<li><a href="/flint-particles/index.html">ABOUT</a></li>
			<li><a href="/flint-particles/source-code.html">SOURCE CODE</a></li>
			<li><a href="/flint-particles/documentation.html">DOCUMENTATION</a></li>
			<li><a href="/flint-particles/examples/">EXAMPLES</a></li>
		</ul>
	</div>
	</nav>
</header>

<div id="main">
<div id="content">


<link type="text/css" href="/flint-particles/css/jquery-ui-1.8.2.custom.css" rel="stylesheet" />
<script type="text/javascript" src="/flint-particles/javascript/jquery-1.4.2.min.js"></script>
<script type="text/javascript" src="/flint-particles/javascript/jquery-ui-1.8.2.custom.min.js"></script>

<h1>Fountain</h1>

<div class="spaced center">
  <object width="500" height="500" data="/flint-particles/source/examples3D/Fountain/PureAS3/Fountain.swf" type="application/x-shockwave-flash">
    <param name="movie" value="/flint-particles/source/examples3D/Fountain/PureAS3/Fountain.swf" />
    <param name="loop" value="false" />
    <param name="quality" value="high" />
    <param name="allowfullscreen" value="true" />
  </object>
</div>

<p>Rendering pixel particles in 3D.</p>

<script type="text/javascript">$(function() { $("#source").tabs(); });</script>

<div id="source">
  <ul><li><a href="#PureAS3">PureAS3</a></li>
<li><a href="#Flash">Flash</a></li>
<li><a href="#Flex">Flex</a></li>
<li><a href="#Papervision3d">Papervision3d</a></li>
</ul><div id="PureAS3"><pre class="code">package
{
  import org.flintparticles.threeD.emitters.Emitter3D;
  import org.flintparticles.threeD.renderers.PixelRenderer;
  import org.flintparticles.threeD.renderers.controllers.OrbitCamera;

  import flash.display.Sprite;
  import flash.filters.BlurFilter;
  import flash.filters.ColorMatrixFilter;
  import flash.geom.Rectangle;
  import flash.geom.Vector3D;

  [SWF(width='500', height='500', frameRate='61', backgroundColor='#000000')]
  
  public class Main extends Sprite
  {
    private var emitter:Emitter3D;
    private var renderer:PixelRenderer;
    private var orbitter:OrbitCamera;
    
    public function Main()
    {
      emitter = new Fountain();
      
      renderer = new PixelRenderer( new Rectangle( -250, -250, 500, 500 ), false );
      renderer.camera.dolly( -300 );
      renderer.camera.lift( 100 );
      renderer.camera.target = new Vector3D( 0, -100, 0 );
      renderer.addFilter( new BlurFilter( 2, 2, 1 ), true );
      renderer.addFilter( new ColorMatrixFilter( [ 1,0,0,0,0,0,1,0,0,0,0,0,1,0,0,0,0,0,0.99,0 ] ), true );
      renderer.addEmitter( emitter );
      renderer.x = 250;
      renderer.y = 250;
      addChild( renderer );
      
      emitter.start( );
      
      orbitter = new OrbitCamera( stage, renderer.camera );
      orbitter.start();
    }
  }
}</pre><pre class="code">package
{
  import org.flintparticles.common.actions.Age;
  import org.flintparticles.common.counters.Steady;
  import org.flintparticles.common.initializers.ColorInit;
  import org.flintparticles.common.initializers.Lifetime;
  import org.flintparticles.threeD.actions.Accelerate;
  import org.flintparticles.threeD.actions.Move;
  import org.flintparticles.threeD.emitters.Emitter3D;
  import org.flintparticles.threeD.initializers.Velocity;
  import org.flintparticles.threeD.zones.DiscZone;

  import flash.geom.Vector3D;

  public class Fountain extends Emitter3D
  {
    public function Fountain()
    {
      counter = new Steady( 2500 );
      
      addInitializer( new ColorInit( 0xFFCCCCFF, 0xFF6666FF ) );
      addInitializer( new Velocity( new DiscZone( new Vector3D( 0, -250, 0 ), new Vector3D( 0, 1, 0 ), 60 ) ) );
      addInitializer( new Lifetime( 3.2 ) );
      
      addAction( new Move() );
      addAction( new Accelerate( new Vector3D( 0, 150, 0 ) ) );
      addAction( new Age() );
    }
  }
}</pre></div>
<div id="Flash"><pre class="code">import org.flintparticles.common.actions.*;
import org.flintparticles.common.counters.*;
import org.flintparticles.common.initializers.*;
import org.flintparticles.threeD.actions.*;
import org.flintparticles.threeD.emitters.Emitter3D;
import org.flintparticles.threeD.initializers.*;
import org.flintparticles.threeD.renderers.PixelRenderer;
import org.flintparticles.threeD.renderers.controllers.*;
import org.flintparticles.threeD.zones.*;  

var emitter:Emitter3D = new Emitter3D();
emitter.counter = new Steady( 2500 );

emitter.addInitializer( new ColorInit( 0xFFCCCCFF, 0xFF6666FF ) );
emitter.addInitializer( new Velocity( new DiscZone( new Vector3D( 0, -250, 0 ), new Vector3D( 0, 1, 0 ), 60 ) ) );
emitter.addInitializer( new Lifetime( 3.2 ) );

emitter.addAction( new Move() );
emitter.addAction( new Accelerate( new Vector3D( 0, 150, 0 ) ) );
emitter.addAction( new Age() );

var renderer:PixelRenderer = new PixelRenderer( new Rectangle( -250, -250, 500, 500 ), false );
renderer.camera.dolly( -300 );
renderer.camera.lift( 100 );
renderer.camera.target = new Vector3D( 0, -100, 0 );
renderer.addFilter( new BlurFilter( 2, 2, 1 ), true );
renderer.addFilter( new ColorMatrixFilter( [ 1,0,0,0,0,0,1,0,0,0,0,0,1,0,0,0,0,0,0.99,0 ] ), true );
renderer.addEmitter( emitter );
renderer.x = 250;
renderer.y = 250;
addChild( renderer );

emitter.start( );

var orbitter:OrbitCamera = new OrbitCamera( stage, renderer.camera );
orbitter.start();</pre></div>
<div id="Flex"><pre class="code">&lt;?xml version=&quot;1.0&quot; encoding=&quot;utf-8&quot;?&gt;
&lt;s:Application
  xmlns:fx=&quot;http://ns.adobe.com/mxml/2009&quot; 
  xmlns:s=&quot;library://ns.adobe.com/flex/spark&quot;
  xmlns:f=&quot;http://flintparticles.org/2009/flint3d&quot;
  xmlns:geom=&quot;flash.geom.*&quot;
  xmlns:filters=&quot;flash.filters.*&quot;
  width=&quot;500&quot; height=&quot;500&quot;
  backgroundColor=&quot;#000000&quot;&gt;

&lt;f:PixelRenderer id=&quot;renderer&quot; width=&quot;500&quot; height=&quot;500&quot;&gt;
  &lt;f:camera&gt;
    &lt;f:Camera&gt;
      &lt;f:position&gt;
        &lt;geom:Vector3D x=&quot;0&quot; y=&quot;-100&quot; z=&quot;-300&quot;/&gt;
      &lt;/f:position&gt;
      &lt;f:target&gt;
        &lt;geom:Vector3D x=&quot;0&quot; y=&quot;-100&quot; z=&quot;0&quot;/&gt;
      &lt;/f:target&gt;
      &lt;f:controller&gt;
        &lt;f:OrbitCamera/&gt;
      &lt;/f:controller&gt;
    &lt;/f:Camera&gt;
  &lt;/f:camera&gt;
  &lt;f:postFilters&gt;
    &lt;filters:BlurFilter blurX=&quot;2&quot; blurY=&quot;2&quot; quality=&quot;1&quot;/&gt;
    &lt;filters:ColorMatrixFilter matrix=&quot;{[ 1,0,0,0,0,0,1,0,0,0,0,0,1,0,0,0,0,0,0.99,0 ]}&quot;/&gt;
  &lt;/f:postFilters&gt;
  &lt;f:emitters&gt;
    &lt;f:Emitter id=&quot;emitter&quot; autoStart=&quot;true&quot;&gt;
      &lt;f:counter&gt;
        &lt;f:Steady rate=&quot;2500&quot;/&gt;
      &lt;/f:counter&gt;
      &lt;f:initializers&gt;
        &lt;f:Velocity&gt;
          &lt;f:DiscZone innerRadius=&quot;0&quot; outerRadius=&quot;60&quot;&gt;
            &lt;f:center&gt;
              &lt;geom:Vector3D x=&quot;0&quot; y=&quot;-250&quot; z=&quot;0&quot;/&gt;
            &lt;/f:center&gt;
            &lt;f:normal&gt;
              &lt;geom:Vector3D x=&quot;0&quot; y=&quot;1&quot; z=&quot;0&quot;/&gt;
            &lt;/f:normal&gt;
          &lt;/f:DiscZone&gt;
        &lt;/f:Velocity&gt;
        &lt;f:ColorInit minColor=&quot;0xFFCCCCFF&quot; maxColor=&quot;0xFF6666FF&quot;/&gt;
        &lt;f:Lifetime lifetime=&quot;3.2&quot;/&gt;
      &lt;/f:initializers&gt;
      &lt;f:actions&gt;
        &lt;f:Move/&gt;
        &lt;f:Age/&gt;
        &lt;f:Accelerate x=&quot;0&quot; y=&quot;150&quot; z=&quot;0&quot; /&gt;
      &lt;/f:actions&gt;
    &lt;/f:Emitter&gt;
  &lt;/f:emitters&gt;
&lt;/f:PixelRenderer&gt;
&lt;/s:Application&gt;</pre></div>
<div id="Papervision3d"><pre class="code">package
{
  import org.flintparticles.integration.papervision3d.PV3DPixelRenderer;
  import org.flintparticles.threeD.emitters.Emitter3D;
  import org.papervision3d.cameras.Camera3D;
  import org.papervision3d.core.effects.BitmapLayerEffect;
  import org.papervision3d.core.geom.Pixels;
  import org.papervision3d.render.BasicRenderEngine;
  import org.papervision3d.scenes.Scene3D;
  import org.papervision3d.view.Viewport3D;
  import org.papervision3d.view.layer.BitmapEffectLayer;

  import flash.display.Sprite;
  import flash.events.Event;
  import flash.filters.BlurFilter;
  import flash.filters.ColorMatrixFilter;

  [SWF(width='500', height='500', frameRate='61', backgroundColor='#000000')]
  
  public class Main extends Sprite
  {
    private var viewport:Viewport3D;
    private var emitter:Emitter3D;
    private var pv3dRenderer:BasicRenderEngine;
    private var flintRenderer:PV3DPixelRenderer;
    private var scene:Scene3D;
    private var camera:Camera3D;
    private var bfx:BitmapEffectLayer;
    private var pixels:Pixels;
    
    public function Main()
    {
      viewport = new Viewport3D( 500, 500 );
      addChild( viewport );
      
      pv3dRenderer = new BasicRenderEngine();
      scene = new Scene3D();
      camera = new Camera3D();
      camera.z = -300;
      
      bfx = new BitmapEffectLayer( viewport, 500, 500 );
      viewport.containerSprite.addLayer( bfx );
      pixels = new Pixels( bfx );
      bfx.addDisplayObject3D( pixels );
      scene.addChild( pixels );
      bfx.addEffect( new BitmapLayerEffect( new BlurFilter( 2, 2, 1 ) ) );
      bfx.addEffect( new BitmapLayerEffect( new ColorMatrixFilter( [ 1,0,0,0,0,0,1,0,0,0,0,0,1,0,0,0,0,0,0.99,0 ] ) ) );
      
      emitter = new Fountain();
      emitter.position.y = -100;

      flintRenderer = new PV3DPixelRenderer( pixels );
      flintRenderer.addEmitter( emitter );
      emitter.start();
      
      addEventListener( Event.ENTER_FRAME, render, false, 0, true );
    }
    
    private function render( ev:Event ):void
    {
      // render the view
      pv3dRenderer.renderScene( scene, camera, viewport);
    }
  }
}</pre><pre class="code">package
{
  import org.flintparticles.common.actions.Age;
  import org.flintparticles.common.counters.Steady;
  import org.flintparticles.common.initializers.ColorInit;
  import org.flintparticles.common.initializers.Lifetime;
  import org.flintparticles.threeD.actions.Accelerate;
  import org.flintparticles.threeD.actions.Move;
  import org.flintparticles.threeD.emitters.Emitter3D;
  import org.flintparticles.threeD.initializers.Velocity;
  import org.flintparticles.threeD.zones.DiscZone;

  import flash.geom.Vector3D;

  public class Fountain extends Emitter3D
  {
    public function Fountain()
    {
      counter = new Steady( 2500 );
      
      addInitializer( new ColorInit( 0xFFCCCCFF, 0xFF6666FF ) );
      addInitializer( new Velocity( new DiscZone( new Vector3D( 0, 250, 0 ), new Vector3D( 0, 1, 0 ), 60 ) ) );
      addInitializer( new Lifetime( 3.2 ) );
      
      addAction( new Move() );
      addAction( new Accelerate( new Vector3D( 0, -150, 0 ) ) );
      addAction( new Age() );
    }
  }
}</pre></div>


</div>
</div>

<footer role="contentinfo">
	<p>Richard Lord - Writer, Choreographer, Game Developer - <a href="/" title="Richard Lord">www.richardlord.net</a></p>
</footer>
</div>
</body>
</html>