<!DOCTYPE html>
<html lang="en">
<head>
<meta http-equiv="Content-Type" content="text/html; charset=UTF-8" />
<meta name="viewport" content="width=device-width, initial-scale=1" />
<title>Flint Particles Explode Image</title>
<link rel="stylesheet" type="text/css" media="all" href="/style.css" />
<link rel="stylesheet" type="text/css" media="all" href="/flint-particles/style.css" />
<link rel="icon" href="/favicon.ico" type="image/x-icon" />
<link rel="shortcut icon" href="/favicon.ico" type="image/x-icon" />
</head>

<body>
<div id="page">
<header role="banner">

	<div id="branding"><img id="site-image" src="/flint-particles/images/core/logobig.gif" width="325" height="37" /></div>

	<nav id="menu" role="navigation">
	<div>
		<ul>
			<li><a href="/flint-particles/index.html">ABOUT</a></li>
			<li><a href="/flint-particles/source-code.html">SOURCE CODE</a></li>
			<li><a href="/flint-particles/documentation.html">DOCUMENTATION</a></li>
			<li><a href="/flint-particles/examples/">EXAMPLES</a></li>
			<li><a href="/flint-particles/blog/">BLOG</a></li>
		</ul>
	</div>
	</nav>
</header>

<div id="main">
<div id="content">


<link type="text/css" href="/flint-particles/css/jquery-ui-1.8.2.custom.css" rel="stylesheet" />
<script type="text/javascript" src="/flint-particles/javascript/jquery-1.4.2.min.js"></script>
<script type="text/javascript" src="/flint-particles/javascript/jquery-ui-1.8.2.custom.min.js"></script>

<h1>Explode Image</h1>

<div class="spaced center">
  <object width="500" height="350" data="/flint-particles/source/examples3D/ExplodeImage/PureAS3/ExplodeImage.swf" type="application/x-shockwave-flash">
    <param name="movie" value="/flint-particles/source/examples3D/ExplodeImage/PureAS3/ExplodeImage.swf" />
    <param name="loop" value="false" />
    <param name="quality" value="high" />
    <param name="allowfullscreen" value="true" />
  </object>
</div>

<p>Here we use one of Flint&#8217;s utility functions to create many small particles from a single image. Then, by applying the Explode activity at the location of the mouse click, we blow the image apart.</p><script type="text/javascript">$(function() { $("#source").tabs(); });</script>
<div id="source">
  <ul><li><a href="#PureAS3">PureAS3</a></li>
<li><a href="#Flash">Flash</a></li>
<li><a href="#Flex">Flex</a></li>
</ul><div id="PureAS3"><pre class="code">package
{
  import org.flintparticles.common.events.EmitterEvent;
  import org.flintparticles.common.particles.Particle;
  import org.flintparticles.threeD.actions.DeathZone;
  import org.flintparticles.threeD.actions.Explosion;
  import org.flintparticles.threeD.actions.Move;
  import org.flintparticles.threeD.emitters.Emitter3D;
  import org.flintparticles.threeD.particles.Particle3DUtils;
  import org.flintparticles.threeD.renderers.DisplayObjectRenderer;
  import org.flintparticles.threeD.zones.FrustrumZone;

  import flash.display.BitmapData;
  import flash.display.Sprite;
  import flash.events.MouseEvent;
  import flash.geom.Point;
  import flash.geom.Rectangle;
  import flash.geom.Vector3D;

  [SWF(width='500', height='350', frameRate='60', backgroundColor='#000000')]
  
  public class Main extends Sprite
  {
    private var emitter:Emitter3D;
    private var bitmap:BitmapData;
    private var renderer:DisplayObjectRenderer;
    private var explosion:Explosion;
    
    public function Main()
    {
      bitmap = new Image1( 384, 255 );
      
      renderer = new DisplayObjectRenderer();
      renderer.camera.dolly( -400 );
      renderer.camera.projectionDistance = 400;
      renderer.y = 175;
      renderer.x = 250;
      addChild( renderer );
      
      emitter = new Emitter3D();
      emitter.addAction( new Move() );
      emitter.addAction( new DeathZone( new FrustrumZone( renderer.camera, new Rectangle( -290, -215, 580, 430 ) ), true ) );
      prepare();
      
      renderer.addEmitter( emitter );
      emitter.start();
      stage.addEventListener( MouseEvent.CLICK, explode, false, 0, true );
      emitter.addEventListener( EmitterEvent.EMITTER_EMPTY, prepare );
    }
    
    private function prepare( event:EmitterEvent = null ):void
    {
      if( explosion )
      {
        emitter.removeAction( explosion );
        explosion = null;
      }
      var particles:Vector.&lt;Particle&gt; = Particle3DUtils.createRectangleParticlesFromBitmapData( bitmap, 12, emitter.particleFactory, new Vector3D( -192, -127, 0 ) );
      emitter.addParticles( particles, false );
    }
    
    private function explode( ev:MouseEvent ):void
    {
      if( !explosion )
      {
        var p:Point = renderer.globalToLocal( new Point( ev.stageX, ev.stageY ) );
        explosion = new Explosion( 8, new Vector3D( p.x, p.y, 50 ), 500 );
        emitter.addAction( explosion );
      }
    }
  }
}</pre></div>
<div id="Flash"><pre class="code">import org.flintparticles.common.events.EmitterEvent;
import org.flintparticles.common.particles.Particle;
import org.flintparticles.threeD.actions.*;
import org.flintparticles.threeD.emitters.Emitter3D;
import org.flintparticles.threeD.particles.Particle3DUtils;
import org.flintparticles.threeD.renderers.DisplayObjectRenderer;
import org.flintparticles.threeD.zones.FrustrumZone;

var explosion:Explosion;
var bitmap:BitmapData = new Image1( 384, 255 );

var renderer:DisplayObjectRenderer = new DisplayObjectRenderer();
renderer.camera.dolly( -400 );
renderer.camera.projectionDistance = 400;
renderer.y = 175;
renderer.x = 250;
addChild( renderer );

var emitter:Emitter3D = new Emitter3D();
emitter.addAction( new Move() );
emitter.addAction( new DeathZone( new FrustrumZone( renderer.camera, new Rectangle( -290, -215, 580, 430 ) ), true ) );
emitter.position = new Vector3D( 0, 0, 0 );
prepare();

renderer.addEmitter( emitter );
emitter.start();

stage.addEventListener( MouseEvent.CLICK, explode, false, 0, true );
emitter.addEventListener( EmitterEvent.EMITTER_EMPTY, prepare );

function prepare( event:EmitterEvent = null ):void
{
  if( explosion )
  {
    emitter.removeAction( explosion );
    explosion = null;
  }
  var particles:Vector.&lt;Particle&gt; = Particle3DUtils.createRectangleParticlesFromBitmapData( bitmap, 12, emitter.particleFactory, new Vector3D( -192, -127, 0 ) );
  emitter.addParticles( particles, false );
}

function explode( ev:MouseEvent ):void
{
  if( !explosion )
  {
    var p:Point = renderer.globalToLocal( new Point( ev.stageX, ev.stageY ) );
    explosion = new Explosion( 8, new Vector3D( p.x, p.y, 50 ), 500 );
    emitter.addAction( explosion );
  }
}</pre></div>
<div id="Flex"><pre class="code">&lt;?xml version=&quot;1.0&quot; encoding=&quot;utf-8&quot;?&gt;
&lt;s:Application
  xmlns:fx=&quot;http://ns.adobe.com/mxml/2009&quot; 
  xmlns:s=&quot;library://ns.adobe.com/flex/spark&quot;
  xmlns:f=&quot;http://flintparticles.org/2009/flint3d&quot;
  xmlns:geom=&quot;flash.geom.*&quot;
  width=&quot;500&quot; height=&quot;350&quot;
  backgroundColor=&quot;#000000&quot; creationComplete=&quot;complete()&quot;&gt;

&lt;fx:Script&gt;
  &lt;![CDATA[
    import org.flintparticles.common.events.EmitterEvent;
    import org.flintparticles.common.particles.Particle;
    import org.flintparticles.threeD.actions.Explosion;
    import org.flintparticles.threeD.particles.Particle3DUtils;
    
    private var explosion:Explosion;
    private var bitmap:BitmapData;
    
    private function complete():void
    {
      bitmap  = new Image1( 384, 255 );
      prepare();
      addEventListener( MouseEvent.CLICK, explode, false, 0, true );
      emitter.addEventListener( EmitterEvent.EMITTER_EMPTY, prepare );
    }
    
    private function prepare( event:EmitterEvent = null ):void
    {
      if( explosion )
      {
        emitter.removeAction( explosion );
        explosion = null;
      }
      var particles:Vector.&lt;Particle&gt; = Particle3DUtils.createRectangleParticlesFromBitmapData( bitmap, 12, emitter.particleFactory, new Vector3D( -192, -127, 0 ) );
      emitter.addParticles( particles, false );
    }

    private function explode( ev:MouseEvent ):void
    {
      if( !explosion )
      {
        var p:Point = renderer.globalToLocal( new Point( ev.stageX, ev.stageY ) );
        explosion = new Explosion( 8, new Vector3D( p.x - renderer.width / 2, p.y - renderer.height / 2, 50 ), 500 );
        emitter.addAction( explosion );
      }
    }
  ]]&gt;
&lt;/fx:Script&gt;

&lt;f:DisplayObjectRenderer x=&quot;0&quot; y=&quot;0&quot; id=&quot;renderer&quot; width=&quot;500&quot; height=&quot;350&quot;&gt;
  &lt;f:camera&gt;
    &lt;f:Camera id=&quot;camera&quot; projectionDistance=&quot;400&quot;&gt;
      &lt;f:position&gt;
        &lt;geom:Vector3D x=&quot;0&quot; y=&quot;0&quot; z=&quot;-400&quot;/&gt;
      &lt;/f:position&gt;
    &lt;/f:Camera&gt;
  &lt;/f:camera&gt;
  &lt;f:emitters&gt;
    &lt;f:Emitter id=&quot;emitter&quot; autoStart=&quot;true&quot;&gt;
      &lt;f:actions&gt;
        &lt;f:Move/&gt;
        &lt;f:DeathZone zoneIsSafe=&quot;true&quot;&gt;
          &lt;f:FrustrumZone camera=&quot;{camera}&quot;&gt;
            &lt;f:viewRect&gt;
              &lt;geom:Rectangle left=&quot;-290&quot; top=&quot;-215&quot; width=&quot;580&quot; height=&quot;430&quot;/&gt;
            &lt;/f:viewRect&gt;
          &lt;/f:FrustrumZone&gt;
        &lt;/f:DeathZone&gt;
      &lt;/f:actions&gt;
    &lt;/f:Emitter&gt;
  &lt;/f:emitters&gt;
&lt;/f:DisplayObjectRenderer&gt;
&lt;/s:Application&gt;</pre></div>


</div>
</div>

<footer role="contentinfo">
	<p>Richard Lord - Game Designer, Programmer, Choreographer - <a href="/" title="Richard Lord">www.richardlord.net</a></p>
</footer>
</div>
</body>
</html>